import java.time.Duration

//
// Shipkit tasks
//

// Plugin jars are added to the buildscript classpath in the root build.gradle file.
apply plugin: "org.shipkit.shipkit-auto-version"  // https://github.com/shipkit/shipkit-auto-version

apply plugin: "org.shipkit.shipkit-changelog"  // https://github.com/shipkit/shipkit-changelog
tasks.named("generateChangelog") {
    previousRevision = project.ext.'shipkit-auto-version.previous-tag'
    githubToken = System.getenv("GITHUB_TOKEN")
    repository = "linkedin/isolation-forest"
}

apply plugin: "org.shipkit.shipkit-github-release"  // https://github.com/shipkit/shipkit-changelog
tasks.named("githubRelease") {
    def genTask = tasks.named("generateChangelog").get()
    dependsOn genTask
    repository = genTask.repository
    changelog = genTask.outputFile
    githubToken = System.getenv("GITHUB_TOKEN")
    newTagRevision = System.getenv("GITHUB_SHA")
}

//
// Maven Central configuration
//

apply plugin: "io.github.gradle-nexus.publish-plugin"  // https://github.com/gradle-nexus/publish-plugin/
nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv("SONATYPE_PORTAL_TOKEN_USER")
            password = System.getenv("SONATYPE_PORTAL_TOKEN_PWD")
        }
    }

    connectTimeout = Duration.ofMinutes(15)
    clientTimeout = Duration.ofMinutes(15)

    transitionCheckOptions {
        // We have many artifacts so Maven Central takes a long time on its compliance checks. This sets the timeout for
        // waiting for the repository to close to a comfortable 60 minutes.
        maxRetries.set(360)
        delayBetween.set(Duration.ofSeconds(10))
    }
}
